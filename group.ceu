class Group with
    event _SDL_MouseButtonEvent* go;
do
    loop do
        var _SDL_MouseButtonEvent* but1 = await go;
        var _SDL_Rect rect;
            rect.w = 0;
            rect.h = 0;
        par/or do
            var int x = but1:x;
            var int y = but1:y;
            par/or do
                var _SDL_MouseMotionEvent* but2;
                every but2 in SDL_MOUSEMOTION do
                    if but2:x > x then
                        rect.x = x;
                        rect.w = but2:x - x;
                    else
                        rect.x = (int)but2:x;
                        rect.w = x - but2:x;
                    end
                    if but2:y > y then
                        rect.y = y;
                        rect.h = but2:y - y;
                    else
                        rect.y = (int)but2:y;
                        rect.h = y - but2:y;
                    end
                end
            with
                var _SDL_MouseButtonEvent* but2;
                every but2 in SDL_MOUSEBUTTONUP do
                    if but2:button == _SDL_BUTTON_LEFT then
                        // select all units in the selected region
                        loop (Unit*)u in global:all do
                            if u:id == _VIS_UNIT1 then
                                u:selected? = _SDL_HasIntersection(&u:rect,&rect);
                            end
                        end
                        break;
                    end
                end
            end
        with
            await SDL_MOUSEMOTION;  // start drawing when has area
            every SDL_REDRAW do
                _SDL_SetRenderDrawColor(_REN, 255,255,255,255);
                _SDL_RenderDrawRect(_REN, &rect);
            end
        end
    end
end
