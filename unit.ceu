class Melee with
    interface Visible;
        var int id    = 0;
        var int team  = 0;
        var int speed = 0;

    var _SDL_Point to;
do
    _assert(this.id     != 0);
    _assert(this.team   != 0);
    _assert(this.speed  != 0);

    var Visible* me = &this;
    var MoveVisible move with
        this.vis = me;
        this.to  = to;
    end;

    var bool hit?;
    par/or do
        var Visible* oth = await this.go_collide
                           until oth:team != this.team;
        emit oth:go_hit => 10;    // TODO: damage
        hit? = true;
    with
        await move.ok;      // missed, no one is there
        hit? = false;
    end

    if hit? then
        _Mix_PlayChannel(-1, _SND_SWORD, 0);
        par/or do
            await 100ms;
        with
            every SDL_REDRAW do
                _filledCircleRGBA(_REN, to.x,to.y,2,
                                        0xCC,0x00,0x00,0xFF);
            end
        end
    end
end

class Unit with
    interface Visible;
        var int id    = 0;
        var int team  = 0;
        var int speed = 0;

    var _SDL_Texture*   tex = null;

    var bool            selected? = 0;  // to receive commands from player
    event _SDL_Point*   go_point;       // move to "_SDL_Point" (null stop)
do
    _assert(this.id     != 0);
    _assert(this.team   != 0);
    _assert(this.speed  != 0);
    _assert(this.tex    != null);
    _assert(this.life   != 0);

    // standing frame
    var _SDL_Rect clip;
        clip.x = 0;
        clip.y = 0;
        clip.w = 32;
        clip.h = 48;

    pool Melee[1] melees;
        // TODO: melee spawn final draw

    par/or do
        // MELEE HIT (go_hit):
        // - take damage
        // - TODO: ignores all but the first
        var int v;
        every v in this.go_hit do
            this.life = life - v;
            if life <= 0 then
                break;
            end
        end
        await 100ms;    // last breath
    with
        // VISIBLE COLLISION (go_collide):
        // - spawn melee attack
        // - TODO: ignores all but the first colliding org
        loop do
            var Visible* oth = await this.go_collide;
            // TODO: track with "new" (if scope is bigger)
            spawn Melee in global:all with
                this.id    = id+1;      // _VIS_MELEEn
                this.team  = team;      // same team
                this.speed = speed*2;
                this.to    := _SDL_Rect2Point(&oth:rect);
                           // point is plain type

                var _SDL_Point fr := _SDL_Rect2Point(&rect);
                                  // point is plain type
                this.rect := _SDL_Point2Rect(&fr, 1);
                          // rect is plain type
            end;
            await 1s;   // TODO: depend on speed
        end
    with
        // MOVEMENT (go_point) //
        var bool       go? = false;
        var _SDL_Point to;
        loop do
            finalize with
                clip.x = 0;     // always terminate on standing position
            end
            par/or do
                var _SDL_Point* ptr = await go_point;
                go? = (ptr != null);
                if ptr != null then
                    to := *ptr;
                       // point is plain type
                end
            with
                if not go? then
                    await FOREVER;
                end
                go? = false;    // not again

                // move "this" to "*to"
                var Unit* me = &this;
                var MoveVisible move with
                    this.vis = me;
                    this.to  = to;  // TODO: := ??
                end;
                // animate sprite frames while moving
                par/or do
                    await move.ok;
                with
                    loop do
                        loop i in 4 do
                            clip.x = clip.w*i;
                            await (10000/this.speed)ms;
                        end
                    end
                end
            end
        end
    with
        every SDL_REDRAW do
            var int w2 = rect.w / 2;
            var int h2 = rect.h / 2;
            if this.selected? then
                _filledCircleRGBA(_REN, rect.x+w2,rect.y+h2, h2,
                                        0x88,0x88,0x88,0x88);
            end
/*
            if pt_goto? then
                _SDL_RenderDrawLine(_REN, rect.x+w2, rect.y+h2,
                                          pt_goto.x+w2, pt_goto.y+h2);
            end
*/
            _SDL_RenderCopy(_REN, this.tex, &clip, &this.rect);
        end
    end
end
